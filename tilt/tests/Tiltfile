load('ext://restart_process', 'docker_build_with_restart')
load('ext://deployment', 'deployment_yaml', 'service_yaml')
parent = load_dynamic("../Tiltfile")

docker_build_with_restart("anvilops/test", "../../",
  dockerfile="./test.Dockerfile",
  entrypoint="npx prisma migrate reset --force && npx vitest run",
  live_update=[
    sync("../../backend/src", "/app/src"),
    sync("../../backend/test", "/app/test")
  ],
  only=["backend", "templates", "openapi"]
)

# Expose a server that hosts a sample Git repository, used to clone a repository with `git clone http://test-file-server.default.svc.cluster.local/sample/.git`
docker_build("anvilops/test-file-server", ".", dockerfile="./file-server/Dockerfile")
k8s_yaml(deployment_yaml("test-file-server", "anvilops/test-file-server", ports=[{"containerPort": 80}]))
k8s_yaml(service_yaml("test-file-server", ports=["80:80"]))

k8s_resource("anvilops-tests", resource_deps=["test-file-server"])

# Build a sample app to test AnvilOps deployments from an existing image
docker_build("anvilops/sample", "sample/", match_in_env_vars=True)

for obj in parent["parsed"]:
  # Copy the AnvilOps deployment, but make it point to our test image and database
  if obj["kind"] == "Deployment" and obj["metadata"]["name"] == "anvilops":
    c = dict(obj["spec"]["template"]["spec"]["containers"][0]) # dict(x) makes a copy of a dict x

    c["image"] = "anvilops/test"
    c["securityContext"] = None
    c["resources"] = None
    c["livenessProbe"] = None

    c["env"] = list(c["env"])
    for i, v in enumerate(c["env"]):
      if v["name"] == "POSTGRES_DB":
        c["env"][i] = {"name": "POSTGRES_DB", "value": "anvilops_test"}
    
    c["env"].append({"name": "DATABASE_URL", "value": "postgresql://$(POSTGRES_USER):$(POSTGRES_PASSWORD)@$(POSTGRES_HOSTNAME)/$(POSTGRES_DB)"})

    # Show Tilt that we're using the anvilops/sample image so that it builds it before deploying the tests
    c["env"].append({"name": "TEST_ANVILOPS_SAMPLE_IMAGE", "value": "anvilops/sample"})
    
    test_job = {
      "apiVersion": "batch/v1",
      "kind": "Job",
      "metadata": {
        "name": "anvilops-tests",
      },
      "spec": {
        "backoffLimit": 0, # Don't retry unless explicitly asked to do so
        "template": {
          "spec": {
            "containers": [c],
            "volumes": obj["spec"]["template"]["spec"]["volumes"],
            "restartPolicy": "Never"
          }
        }
      }
    }

    k8s_yaml(encode_yaml_stream([test_job]))

